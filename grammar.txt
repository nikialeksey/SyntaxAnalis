main_program ::= 'data_type' 'identifier' description main_program | 'data_type' 'identifier' description

description ::= descriptionF | descriptionV | ';'
descriptionV ::= '=' expression EV  ';' | EV  ';' | '=' expression ';' 
descriptionF ::= '(' ')' body_function | '(' PEVF ')' body_function

EV ::= ',' 'identifier' EV | ',' 'identifier' '=' expression EV | ',' 'identifier' | ',' 'identifier' '=' expression

PEVF ::= param | param EVF
EVF ::= ',' param EVF | ',' param
param ::= 'data_type' 'identifier'

body_function ::= '{' '}' | '{' enum_operator '}' 
enum_operator ::= O enum_operator | O

O ::= operator | 'data_type' 'identifier' descriptionV | 'data_type' 'identifier' ';' 

operator ::= body_function | expression ';' | 'do' operator 'while' '(' expression ')' ';' | 'return' expression ';' | ';'

expression ::= A2 '=' expression | A2 '+=' expression | A2 '-=' expression | A2 '*=' expression | A2 '/=' expression | A2 '%=' expression | A2
A2 ::= A2 '==' A3 | A2 '!=' A3 | A3
A3 ::= A3 '>' A4 | A3 '<' A4 | A3 '<=' A4 | A3 '>=' A4 | A4
A4 ::= A4 '+' A5 | A4 '-' A5 | A5
A5 ::= A5 '*' A6 | A5 '/' A6 | A5 '%' A6 | A6
A6 ::= '-' A7 | '+' A7 | '--' A7 | '++' A7 | A7 '--' | A7 '++' | A7
A7 ::= '(' expression ')' | 'identifier' | 'decimal' | 'hex' | 'identifier' '(' ')'